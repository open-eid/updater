<job>
<?job debug="true" error="true"?>
<script language="vbscript">
function gp(sess,prop)
	gp = " " & prop & "=""" & sess.Property(prop) & """"
end function

	const io_forWriting = 2

	set fso = createobject("scripting.filesystemobject")
	
	set ts = fso.OpenTextFile("products.xml",io_forWriting,true,0)
	ts.write("<?xml version=""1.0"" encoding=""UTF-8"" ?>")
	ts.write("<products>")
	set fol = fso.GetFolder(".")
	for each f in fol.files
		strN = fso.getAbsolutePathName(f.name)
		if right(strN,4)=".msi" then 
			wsh.echo strN
			set installer = createobject("windowsinstaller.installer")
			set sess = installer.openpackage(strN,1)
			WScript.Echo(sess.Property("ProductName"))
			ts.write("<product filename=""" & f.name & """" & _
				gp(sess,"ProductName") &_
				gp(sess,"ProductVersion") &_
				gp(sess,"Manufacturer") &_
				gp(sess,"ProductCode") &_
				gp(sess,"UpgradeCode") &_
				 "></product>")
			set sess = nothing
			set installer = nothing
		end if
	next
	ts.write("</products>")
	ts.close()	
</script>

</job>